{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "mutable struct TreeNode\n",
    "  parent::Int             # dans l'arbre, indice du noeud parent\n",
    "  children::Vector{Int}   # dans l'arbre, indices de noeuds enfants\n",
    "  ub::Float32             # borne supérieure du noeud\n",
    "  setzero::Array{Tuple}   # liste des arcs non utilisés (à 0) à cause des règles de branchement\n",
    "  setone::Array{Tuple}    # liste des arcs obligatoirement utilisés (à 1) à cause des règles de branchement\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Filtrer les colonnes par rapport aux contraintes"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function calculate_columns(node)\n",
    "    node_pool = Array{Dict{String,Any},1}() # pool de colonnes (i.e des cycles) pour ce noeud\n",
    "    \n",
    "    # Colonne artificielle     \n",
    "    node_pool = vcat(node_pool, column_pool[1:KEEP_INIT_COLUMNS])\n",
    "    \n",
    "    for c in (KEEP_INIT_COLUMNS+1):size(column_pool,1)\n",
    "        add = true\n",
    "        \n",
    "        # Vérification que la condition de branchement des arcs non-utilisés est vérifiée\n",
    "        for (i,j) in tree[node].setzero\n",
    "            if (i,j) in column_pool[c][\"oneedges\"]\n",
    "                add = false\n",
    "                @goto next_cycle\n",
    "            end\n",
    "        end\n",
    "        # Et vérification que la condition de branchement des arcs obligatoirement utilisés est vérifiée\n",
    "        for (i,j) in tree[node].setone\n",
    "            if !((i,j) in column_pool[c][\"oneedges\"])\n",
    "                add = false\n",
    "                @goto next_cycle\n",
    "            end\n",
    "        end\n",
    "\n",
    "        if add\n",
    "            push!(node_pool, column_pool[c])\n",
    "        end\n",
    "        @label next_cycle\n",
    "    end\n",
    "\n",
    "    return node_pool\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Tranformation d'une solution maître sous forme z en solution sous forme x\n",
    "function calculate_xsol(mastersol, node_pool)\n",
    "    x = Dict{Tuple{Int,Int},Float32}()\n",
    "    \n",
    "    for e in edges(GRAPH)\n",
    "        i,j = e.src,e.dst\n",
    "        x[(i,j)] = sum([ mastersol[c] for c in 1:size(mastersol,1) if (i,j) in node_pool[c][\"oneedges\"] ])\n",
    "    end  \n",
    "    return x\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function Process_Node(nodeindex)\n",
    "    println(\"\\e[93mNOEUD n°$nodeindex :\\e[00m\")\n",
    "    global nodestobedeleted = []\n",
    "    \n",
    "    # Récupération des colonnes compatibles avec les contraintes de branchement\n",
    "    node_pool = calculate_columns(nodeindex)\n",
    "    \n",
    "    # Création du problème maître restreint\n",
    "    # (on récupère les références des contraintes et des variables z ainsi que le modèle en lui-même)\n",
    "    cyclechoice, z, mastermodel = node_master(node_pool)\n",
    "    \n",
    "    \n",
    "    # GÉNÉRATION DE COLONNES\n",
    "    while true\n",
    "        \n",
    "        # RÉSOLUTION DU PROBLÈME MAÎTRE RESTREINT\n",
    "        optimize!(mastermodel)\n",
    "        \n",
    "        # si le maître restreint est infaisable alors ce noeud est élagé\n",
    "        if termination_status(mastermodel) == MOI.INFEASIBLE \n",
    "            println(\" \\e[93m|\\e[31m  Ce noeud est irréalisable et donc élagué \\e[00m\")\n",
    "            return []\n",
    "        end\n",
    "        \n",
    "        # Récupération du problème maître restreint\n",
    "        #  - la valeur optimale :\n",
    "        value = JuMP.objective_value(mastermodel)\n",
    "        \n",
    "        #  - la solution optimale :\n",
    "        solution = Array{Float32,1}(undef,size(node_pool,1))\n",
    "        for c in 1:size(node_pool,1)\n",
    "            solution[c] = JuMP.value.(z)[c]\n",
    "        end\n",
    "        solflat = collect(Iterators.flatten(solution))\n",
    "        \n",
    "        #  - les solutions duales optimales associées aux contraintes :\n",
    "        π = -JuMP.dual.(cyclechoice)                                ##### le - ici à cause de la maximisation\n",
    "\n",
    "        \n",
    "        # La solution est-elle entière ?\n",
    "        #   si oui, alors on met à jour la borne primale\n",
    "        if maximum(solflat - floor.(solflat)) <= ϵ\n",
    "            # mise à jour avec la meilleure borne primale connue (borne inférieure ici)\n",
    "            if value > LB                                                   ############# j'ai mis > au lieu de >=\n",
    "                global LB = value\n",
    "                println(\" \\e[93m|\\e[32m  Meilleure solution réalisable (entière) de valeur $value trouvée \\e[00m\")\n",
    "            end\n",
    "            \n",
    "            # et on retirera les noeuds non traitées dont les bornes supérieures ne peuvent pas être meilleure\n",
    "            # que la borne inférieure du problème entier\n",
    "            for i in 1:length(Queue)-1                                     \n",
    "                if tree[Queue[i]].ub <= LB                                   ############### À mettre dans le if ci-dessus ?\n",
    "                    push!(nodestobedeleted,i)\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        \n",
    "        \n",
    "        # RÉSOLUTION DU SOUS-PROBLÈME\n",
    "        # récupération de la valeur objectif du sous-problème et de la colonne associée\n",
    "        SPobj, column = subproblem(π, nodeindex)\n",
    "        \n",
    "        # si le sous-problème n'est pas réalisable alors ce noeud est élagé \n",
    "        if SPobj == -Inf\n",
    "            println(\" \\e[93m|\\e[31m  Ce noeud est irréalisable et donc élagué \\e[00m\")\n",
    "            return []\n",
    "        end\n",
    "        \n",
    "        \n",
    "        # MISE À JOUR DE BORNES        \n",
    "        nodelb = value                                                         ########## Vient du problème restreint\n",
    "        nodeub = sum(π) + (nv(GRAPH)/2)*SPobj                                  ########## Vient du Lagrangien dual\n",
    "\n",
    "                                                                  \n",
    "        # Si le coût réduit de la colonne est positive alors on l'ajoute\n",
    "        # (à noter 'SPobj' est le coût réduit ici)\n",
    "        if SPobj > 0                                                           ##################### strictement ?\n",
    "            # Ajout de la colonne au pool de toutes les colonnes ainsi qu'au pool de colonnes de ce noeud\n",
    "            column[\"cost\"] = sum(WEIGHTS[(i,j)] for (i,j) in column[\"oneedges\"])\n",
    "            push!(column_pool, column)\n",
    "            push!(node_pool, column)\n",
    "\n",
    "            # Ajout de la colonne dans le modèle\n",
    "            #   une variable z est d'abord ajoutée\n",
    "            push!(z, @variable(mastermodel,lower_bound=0,upper_bound=1))\n",
    "            set_name(z[end], \"z_$(size(node_pool,1))\") # nom de la variable\n",
    "\n",
    "            #   ajout ensuite des coefficients de la variable dans les contraintes de choix de cycles\n",
    "            for i in VERTICES\n",
    "                set_normalized_coefficient(cyclechoice[i], z[end], column[\"vertices\"][i])\n",
    "            end\n",
    "\n",
    "            #   mise à jour de la fontion objectif\n",
    "            column_cost = column[\"cost\"]\n",
    "            set_objective_function(mastermodel, objective_function(mastermodel) + column_cost*z[end])\n",
    "\n",
    "            # Affichage du cycle ajouté\n",
    "            cycle = findall(y -> y!=0, column[\"vertices\"])\n",
    "            push!(cycle, cycle[1])\n",
    "            # println(\" \\e[93m|\\e[00m  Ajout du cycle $cycle \\e[00m\")\n",
    "        end\n",
    "        \n",
    "        \n",
    "        # Si une meilleur borne supérieure a été trouvé lors de l'ajout de la colonne,\n",
    "        # alors on met à jour la borne supérieure de ce noeud\n",
    "        if nodeub <= tree[nodeindex].ub\n",
    "            tree[nodeindex].ub = nodeub\n",
    "        end\n",
    "        \n",
    "        \n",
    "        # CONVERGENCE DE L'ALGORITHME\n",
    "        # quand l'algorithme a convergé (écart relatif entre les bornes épslionesque ou coût réduit négatif ou nul)\n",
    "        if 2*abs((tree[nodeindex].ub - nodelb))/abs((tree[nodeindex].ub + nodelb)) < ϵ || SPobj <= 0\n",
    "            println(\" \\e[93m|\\e[00m  Relaxation du noeud résolue à l'optimalité avec la borne inférieure : $nodelb\")\n",
    "            println(\" \\e[93m|\\e[00m                                               et la borne supérieure : $(tree[nodeindex].ub)\")\n",
    "            \n",
    "            # Si la colonne artificielle est utilisée dans la solution,       ##################### Revoir\n",
    "            # alors ce noeud est irréalisable\n",
    "            if solution[KEEP_INIT_COLUMNS] >= ϵ\n",
    "                println(\" \\e[93m|\\e[31m  Ce noeud est irréalisable et donc élagué (la colonne artificielle est encore présente)\\e[00m\")\n",
    "                return []\n",
    "            end\n",
    "\n",
    "            # Sinon, on retourne la solution de ce noeud à l'aide de la fonction 'calculate_xsol',\n",
    "            # qui renvoie les variables x_ij des arcs   \n",
    "            return calculate_xsol(solution, node_pool)\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.3",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
